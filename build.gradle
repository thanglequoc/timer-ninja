plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id 'io.freefair.aspectj' version '9.1.0'
    id 'org.jreleaser' version '1.21.0'
}

group 'io.github.thanglequoc'
version '1.2.0'

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = "17"
    targetCompatibility = "17"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.aspectj', name: 'aspectjrt', version: '1.9.25'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '[2.0.17,)'

    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.20.0'
    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.21'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.21'
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'timer-ninja'
            from components.java

            pom {
                name = 'Timer Ninja'
                description = 'Timer Ninja is a lightweight Java library for measuring method execution time with ease. It provides annotation-based tracking and a flexible API to measure execution time, supports different time units, and visualizes the tracking context in stacktrace tree format.'
                url = 'https://github.com/ThangLeQuoc/timer-ninja'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'thanglequoc'
                        name = 'Thang Le Quoc'
                        email = 'thanglequoc.it@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ThangLeQuoc/timer-ninja.git'
                    developerConnection = 'scm:git:ssh://github.com/ThangLeQuoc/timer-ninja.git'
                    url = 'https://github.com/ThangLeQuoc/timer-ninja'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
        mavenLocal()
    }
}

jreleaser {
    project {
        name = 'timer-ninja'
        authors = ['ThangLeQuoc']
        maintainers = ['ThangLeQuoc']
        license = 'Apache-2.0'
        links {
            homepage = 'https://github.com/ThangLeQuoc/timer-ninja'
        }
        description = 'A handy Java utility to help you measure execution time of Java code'
        longDescription = 'Timer Ninja is a lightweight Java library for effortlessly measuring method execution time. Just annotate your methods, and it automatically tracks duration, preserves the full call hierarchy, and outputs a clear, visual call tree with detailed timing and argument information.'
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    username = 'changeit'
                    password = 'changeit'
                    authorization = 'BEARER'
                    sign = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }

    signing {
        active = 'RELEASE'
        armored = true
        mode = 'FILE'
        files = true
        artifacts = true
        checksums = true

        passphrase = 'changeit'
        publicKey = './public_key.asc'
        secretKey = './secret_key.asc'

    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
